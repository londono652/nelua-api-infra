# WAF asociado directamente al ALB.

resource "aws_wafv2_ip_set" "load_test" {
  count = length(var.load_test_cidrs) > 0 ? 1 : 0

  name               = "${var.project}-load-test"
  scope              = "REGIONAL"
  ip_address_version = "IPV4"
  addresses          = var.load_test_cidrs
}

resource "aws_wafv2_web_acl" "api" {
  name  = var.project
  scope = "REGIONAL"

  default_action {
    allow {}
  }

  # Excepción temporal para el generador de la prueba de carga.
  dynamic "rule" {
    for_each = aws_wafv2_ip_set.load_test

    content {
      name     = "allow-load-test"
      priority = 0

      action {
        allow {}
      }

      statement {
        ip_set_reference_statement {
          arn = rule.value.arn
        }
      }

      visibility_config {
        cloudwatch_metrics_enabled = true
        metric_name                = "${var.project}-allow-load-test"
        sampled_requests_enabled   = true
      }
    }
  }

  # Límite de peticiones por IP.
  rule {
    name     = "rate-limit-per-ip"
    priority = 1

    action {
      block {}
    }

    statement {
      rate_based_statement {
        limit              = var.waf_rate_limit
        aggregate_key_type = "IP"
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "${var.project}-rate-limit"
      sampled_requests_enabled   = true
    }
  }

  # Reglas administradas por AWS: ataques web comunes (OWASP Top 10).
  rule {
    name     = "aws-common-rules"
    priority = 2

    override_action {
      none {}
    }

    statement {
      managed_rule_group_statement {
        name        = "AWSManagedRulesCommonRuleSet"
        vendor_name = "AWS"
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "${var.project}-common-rules"
      sampled_requests_enabled   = true
    }
  }

  # Reglas administradas por AWS: entradas maliciosas conocidas.
  rule {
    name     = "aws-known-bad-inputs"
    priority = 3

    override_action {
      none {}
    }

    statement {
      managed_rule_group_statement {
        name        = "AWSManagedRulesKnownBadInputsRuleSet"
        vendor_name = "AWS"
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "${var.project}-known-bad-inputs"
      sampled_requests_enabled   = true
    }
  }

  visibility_config {
    cloudwatch_metrics_enabled = true
    metric_name                = var.project
    sampled_requests_enabled   = true
  }
}

resource "aws_wafv2_web_acl_association" "api" {
  resource_arn = aws_lb.api.arn
  web_acl_arn  = aws_wafv2_web_acl.api.arn
}
